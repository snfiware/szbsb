apply plugin: 'com.android.application'

apply plugin: 'com.chaquo.python'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'


// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("secret/keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


// Helper function for adding build timestamp to the version name
def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmmss')
    return formattedDate
}

// This switch is evaluated in the signing and build configuration and aims for the target to
// use just only one build.gradle for public and private builds. For details about
// problems encountered see comments at the usages of this variable. In brief: with this
// switch set to true the installation from APK file (without play store) will work,
// otherwise it may not.
def bBuildForPublic = true // true or false
// We do not ship the signing infos to the public - therefore release builds from github clones
// will not work properly. Use debug build instead, change active build variant to debug. And
// set this variable to true so android studio will use the debug standard signing.

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    flavorDimensions "default"
    defaultConfig {
        python {
            buildPython "/usr/bin/python3"
//            buildPython "py -3.7"
            pip {
                install "requests"
            }
        }
        applicationId "de.snfiware.szbsb"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName '1.1.1'
//        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters "armeabi-v7a"
            abiFilters "arm64-v8a"
            abiFilters "x86"
            abiFilters "x86_64"
        }
        vectorDrawables {
            useSupportLibrary true
        }
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            v1SigningEnabled true
            // Weird error when enabled v2 on installing from apk (without adb)
            //v2SigningEnabled true
            // But with v2 disabled adb install on api 28 fails
            //v2SigningEnabled false
            if( bBuildForPublic ) {
                v2SigningEnabled false // installing from apk file works on api 19, 23, 28
            } else {
                v2SigningEnabled true  // installing with adb and android studio will work
            }
        }
    }
    buildTypes {
        debug {
            versionNameSuffix = "-build" + getDate()
            if( bBuildForPublic ) {
                // use no explicit signing configuration -> android studio will do for us
            } else {
                signingConfig signingConfigs.release
            }
        }
        release {
            versionNameSuffix = "-release" + getDate()
            //minifyEnabled true // leads to crash when loading pdfium and when loading chaquo
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.61"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.github.barteksc:android-pdf-viewer:3.2.0-beta.1'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
}
